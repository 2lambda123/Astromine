buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "maven-publish"
    id "architectury-plugin" version "3.1-SNAPSHOT"
    id "dev.architectury.loom" version "0.7.2-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        // Minecraft
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"

        // Yarn
        mappings "net.fabricmc:yarn:${rootProject.yarn_version}:mergedv2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.mod_name
    version = rootProject.mod_version
    group = rootProject.mod_group

    repositories {
        maven {
            name = "Architectury"
            url = "https://maven.architectury.dev"
        }

        maven {
            name = "TerraFormersMC"
            url = "https://maven.terraformersmc.com/releases/"
        }

        maven {
            name = "JitPack"
            url = "https://jitpack.io"
            
            content {
                includeGroupByRegex "com.github.\\w+"
            }
        }

        maven {
            name = "Ladysnake Libs"
            url = "https://ladysnake.jfrog.io/artifactory/mods"
            
            content {
                includeGroup "io.github.onyxstudios.Cardinal-Components-API"
            }
        }

        maven {
            name = "BlameJared"
            url = "https://maven.blamejared.com"
            
            content {
                includeGroup "vazkii.patchouli"
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 11
    }

    java {
        withSourcesJar()
    }
}