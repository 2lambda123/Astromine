plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '5.1.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.mod_name + "-" + project.mod_version + "+" + project.version_meta
group = project.mod_group

spotless {
    encoding 'UTF-8'
    enforceCheck(false)

    java {
        licenseHeaderFile(file('HEADER'))
        removeUnusedImports()
        eclipse().configFile(file('spotless/formatter-settings.xml'))
    }
}

repositories {
    maven {
        name = "Onyx Studios"
        url = "https://maven.onyxstudios.dev"
    }
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
    maven {
        name = "Jared"
        url = "https://maven.blamejared.com"
    }
    maven {
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name = "SonaType"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name = "Ladysnake Libs"
        url = 'https://dl.bintray.com/ladysnake/libs'
    }
    maven {
        name = "Wrenchable"
        url "https://dl.bintray.com/zundrel/wrenchable"
    }
    maven {
        name = "grondag"
        url = "https://grondag-repo.appspot.com"
        credentials {
            username "guest"
            password ""
        }
    }
}

minecraft {
    accessWidener = file("src/main/resources/astromine.accesswidener")
}

dependencies {
    // Minecraft
    minecraft "com.mojang:minecraft:${minecraft_version}"

    // Yarn
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"

    // Fabric Loader
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"

    // Fabric API
    modApi "net.fabricmc.fabric-api:fabric-api:${api_version}"

    // Cardinal-Components API
    useAsApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${cardinal_components_version}"
    useAsApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:${cardinal_components_version}"
    useAsApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-chunk:${cardinal_components_version}"
    useAsApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${cardinal_components_version}"
    useAsApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-item:${cardinal_components_version}"
    useAsApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-level:${cardinal_components_version}"
    useAsApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-world:${cardinal_components_version}"
    useAsApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-util:${cardinal_components_version}"

    // Shapes
    useAsApi "com.terraformersmc:shapes:${shapes_version}"

    // Blade
    useAsApi "com.github.vini2003:Blade:${blade_version}"

    // Patchouli
    useAsApi "vazkii.patchouli:Patchouli:${patchouli_version}"

    // ModMenu
    useAsApiOptionally "io.github.prospector:modmenu:${modmenu_version}"

    // AutoConfig
    useAsApi "me.sargunvohra.mcmods:autoconfig1u:${auto_config_version}"

    // LBA
    useAsApi "alexiil.mc.lib:libblockattributes-core:${lba_version}"
    useAsApi "alexiil.mc.lib:libblockattributes-items:${lba_version}"
    useAsApi "alexiil.mc.lib:libblockattributes-fluids:${lba_version}"

    // Tech Reborn Energy
    useAsApi "teamreborn:energy:${tr_energy_version}"

    // Cloth API
    useAsApi "me.shedaniel.cloth.api:cloth-durability-bar-api-v1:${cloth_api_version}"

    // Cloth Config
    useAsApi "me.shedaniel.cloth:config-2:${cloth_config_version}"

    // RoughlyEnoughItems
    useAsApiOptionally("me.shedaniel:RoughlyEnoughItems:${rei_version}", false)

    // DataBreaker
//    useAsRuntime "com.github.SuperCoder7979:databreaker:${databreaker_version}"

    // LibStructure
    useAsApi "com.github.Draylar:LibStructure:${libstructure_version}"

    // JOML
    compile "org.joml:joml:${joml_version}"

    // Magna
    useAsApi "com.github.Draylar:magna:${magna_version}"

    // Tech Reborn
//    useAsRuntime "TechReborn:TechReborn-1.16:${tech_reborn_version}"

    // Reborn Core
//    useAsRuntime "RebornCore:RebornCore-1.16:${reborn_core_version}"

    // Wrenchable
    useAsApi "com.zundrel:wrenchable:${wrenchable_version}"
    
    useAsApi "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
}

def useAsApi(dep) {
    useAsApiOptionally(dep)
    dependencies.include(dep)
}

def useAsApiOptionally(dep, removeREI = true) {
    dependencies.modApi(dep) {
        exclude group: "net.fabricmc.fabric-api"
        if (removeREI) {
            exclude group: "me.shedaniel"
        }
    }
}

def useAsRuntime(dep, removeREI = true) {
    dependencies.modRuntime(dep) {
        exclude group: "net.fabricmc.fabric-api"
        if (removeREI) {
            exclude group: "me.shedaniel"
        }
    }
}

processResources {
    inputs.property "version", "${mod_version}"

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": "${mod_version}"
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
    manifest {
        attributes([
                "Specification-Title"     : "${mod_name}",
                "Specification-Vendor"    : "${mod_group}",
                "Specification-Version"   : "${mod_version}",
                "Implementation-Title"    : "${mod_name}",
                "Implementation-Version"  : "${mod_version}",
                "Implementation-Vendor"   : "${mod_group}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "${mod_name}.mixins.json"
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        mavenLocal()
    }
}
